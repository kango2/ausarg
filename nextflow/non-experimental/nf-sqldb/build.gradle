/*
 * Copyright 2020-2022, Seqera Labs
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'groovy'
    id 'idea'
    id 'de.undercouch.download' version '4.1.2'
}

group = 'io.nextflow'
// DO NOT SET THE VERSION HERE
// THE VERSION FOR PLUGINS IS DEFINED IN THE `/resources/META-INF/MANIFEST.NF` file
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

idea {
    module.inheritOutputDirs = true
}

repositories {
    mavenCentral()
    maven { url = 'https://s3-eu-west-1.amazonaws.com/maven.seqera.io/releases' }
    maven { url = 'https://s3-eu-west-1.amazonaws.com/maven.seqera.io/snapshots' }
}

configurations {
    // see https://docs.gradle.org/4.1/userguide/dependency_management.html#sub:exclude_transitive_dependencies
    runtimeClasspath.exclude group: 'org.slf4j', module: 'slf4j-api'
}

sourceSets {
    main.java.srcDirs = []
    main.groovy.srcDirs = ['src/main']
    main.resources.srcDirs = ['src/resources']
    test.groovy.srcDirs = ['src/test']
    test.java.srcDirs = []
    test.resources.srcDirs = []
}

ext{
    nextflowVersion = '22.08.1-edge'
}

dependencies {
    compileOnly "io.nextflow:nextflow:$nextflowVersion"
    compileOnly 'org.slf4j:slf4j-api:1.7.10'
    compileOnly 'org.pf4j:pf4j:3.4.1'

    api("org.codehaus.groovy:groovy-sql:3.0.10") { transitive = false }
    api 'com.h2database:h2:1.4.200'
    api 'mysql:mysql-connector-java:8.0.22'
    api 'org.mariadb.jdbc:mariadb-java-client:2.7.0'
    api 'org.postgresql:postgresql:42.2.23'
    api 'org.xerial:sqlite-jdbc:3.36.0.3'
    api 'org.duckdb:duckdb_jdbc:0.3.0'


    // JDBC driver setup for AWS Athena - the 3rd party JAR are being downloaded and setup as gradle tasks below.
    // Reference https://docs.aws.amazon.com/athena/latest/ug/connect-with-jdbc.html
    api files('src/dist/lib/AthenaJDBC42_2.0.25.1001.jar')

    //NOTE: Had to remove the slf4j jar due to a conflict
    implementation fileTree(dir: 'src/dist/lib/libs', include: '*.jar')


    testImplementation "io.nextflow:nextflow:$nextflowVersion"
    testImplementation "org.codehaus.groovy:groovy:3.0.10"
    testImplementation "org.codehaus.groovy:groovy-nio:3.0.10"
    testImplementation("org.codehaus.groovy:groovy-test:3.0.10") { exclude group: 'org.codehaus.groovy' }
    testImplementation("cglib:cglib-nodep:3.3.0")
    testImplementation("org.objenesis:objenesis:3.2")
    testImplementation("org.spockframework:spock-core:2.1-groovy-3.0") {
        exclude group: 'org.codehaus.groovy';
        exclude group: 'net.bytebuddy'
    }
    testImplementation('org.spockframework:spock-junit4:2.1-groovy-3.0') {
        exclude group: 'org.codehaus.groovy';
        exclude group: 'net.bytebuddy'
    }
    testImplementation('com.google.jimfs:jimfs:1.1')

    testImplementation(testFixtures("io.nextflow:nextflow:$nextflowVersion"))
    testImplementation(testFixtures("io.nextflow:nf-commons:$nextflowVersion"))
}

test {
    useJUnitPlatform()
}

/**
 * AWS Athena
 * The following tasks download and confirm the MD5 checksum of the ZIP archive
 * for Simba Athena JDBC driver and extract its contents to the build directory
 * Reference: https://docs.aws.amazon.com/athena/latest/ug/connect-with-jdbc.html
 */
task downloadAthenDep(type: Download) {
    src 'https://s3.amazonaws.com/athena-downloads/drivers/JDBC/SimbaAthenaJDBC-2.0.25.1001/SimbaAthenaJDBC-2.0.25.1001.zip'
    dest new File(buildDir, 'downloads/SimbaAthenaJDBC-2.0.25.1001.zip')
    overwrite false
}

task verifyAthenDep(type: Verify, dependsOn: downloadAthenDep) {
    src new File(buildDir, 'downloads/SimbaAthenaJDBC-2.0.25.1001.zip')
    algorithm 'MD5'
    checksum '71504d0317656d790978363358d0c068'
}

task unzipAthenDep(dependsOn: verifyAthenDep, type: Copy) {
    from zipTree(new File(buildDir, 'downloads/SimbaAthenaJDBC-2.0.25.1001.zip'))
    into "${buildDir}/downloads/unzip/awsathena/"
}

// Files under src/dist are included into the distribution zip
// https://docs.gradle.org/current/userguide/application_plugin.html
task copyAthenDep(dependsOn: unzipAthenDep, type: Copy) {
    from file(new File(buildDir, '/downloads/unzip/awsathena/SimbaAthenaJDBC-2.0.25.1001/AthenaJDBC42_2.0.25.1001.jar'))
    into "src/dist/lib"
}
project.copyPluginLibs.dependsOn('copyAthenDep')
project.compileGroovy.dependsOn('copyAthenDep')
